import java.text.SimpleDateFormat

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion sdk_target
    defaultConfig {
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
    }

    Date date = Calendar.getInstance().getTime()
    String time = new SimpleDateFormat("EEE MMMM dd HH:mm:ss zzz yyyy", Locale.ENGLISH).format(date)
    String timeYear = new SimpleDateFormat("yyyy", Locale.ENGLISH).format(date)

    buildTypes {
        release {
        }
    }

    // Add config fields to each of build types.
    buildTypes.each {
        it.buildConfigField "String", "MY_TIME", "\"" + time + "\""
        it.buildConfigField "String", "MY_TIME_YEAR", "\"" + timeYear + "\""

        // api keys
        it.buildConfigField "String", "API_OPEN_WEATHER_MAP", "\"${System.env.API_OPEN_WEATHER_MAP}\""
        it.buildConfigField "String", "API_ACCU_WEATHER", "\"${System.env.API_ACCU_WEATHER}\""
        it.buildConfigField "String", "API_DARK_SKY", "\"${System.env.API_DARK_SKY}\""
        it.buildConfigField "String", "API_WEATHER_STACK", "\"${System.env.API_WEATHER_STACK}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    api "com.artemchep.config:config:$config_version"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    implementation "io.arrow-kt:arrow-optics:$arrow_version"
    implementation "io.arrow-kt:arrow-core:$arrow_version"
    kapt "io.arrow-kt:arrow-meta:$arrow_version"

    implementation "com.squareup.moshi:moshi-kotlin:1.12.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.12.0"

    implementation "androidx.core:core-ktx:$ktx_lib_version"
    implementation "androidx.appcompat:appcompat:$appcompat_lib_version"
    implementation "androidx.wear:wear:$wear_lib_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_lib_version"
    api "com.github.AChep:bindin:1.4.0"
    api "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    implementation 'com.google.android.gms:play-services-location:19.0.1'
    implementation "com.google.android.material:material:$material_version"

    implementation "com.google.android.support:wearable:$wearable_version"
    implementation "com.google.android.gms:play-services-wearable:17.1.0"
    compileOnly "com.google.android.wearable:wearable:$wearable_version"
}
